PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ccf: <http://purl.org/ccf/>

PREFIX PART_OF: <http://purl.org/ccf/ccf_part_Of>
PREFIX LOCATED_IN: <http://purl.org/ccf/ccf_located_in>
PREFIX CHARACTERIZES: <http://purl.org/ccf/ccf_characterizes>
PREFIX HAS_MODIFIER: <https://purl.humanatlas.io/vocab/hp#has_modifier>
PREFIX expression_level: <http://purl.obolibrary.org/obo/OBI_0001938>

PREFIX HRAkda: <https://purl.humanatlas.io/graph/hra-kidney-disease-atlas>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>
PREFIX HPO: <https://purl.humanatlas.io/vocab/hp>

SELECT ?mean_gene_expr ?gene ?ct ?as ?phenotype ?gene_label ?ct_label ?as_label ?phenotype_label
FROM HRAkda:
WHERE {
  {
    SELECT DISTINCT ?ct ?gene (AVG(?exp) as ?mean_gene_expr) (COUNT(DISTINCT(?source)) as ?num_sources)
    WHERE {
      [] a rdf:Statement ;
        rdf:subject ?gene ;
        rdf:predicate CHARACTERIZES: ;
        rdf:object ?ct ;
        rdfs:isDefinedBy ?source ;
        expression_level: ?exp .
    }
    GROUP BY ?ct ?gene
    HAVING (?mean_gene_expr > 1.0)
  }

  ?ct LOCATED_IN: ?as .
  ?gene HAS_MODIFIER: ?phenotype .

  GRAPH HRA: {
    ?as rdfs:label ?as_label .
    ?ct rdfs:label ?ct_label .
    ?gene rdfs:label ?gene_label .
  }
  GRAPH HPO: {
    ?phenotype rdfs:label ?phenotype_label .
  }
}
ORDER BY ?as ?ct DESC(?mean_gene_expr)
