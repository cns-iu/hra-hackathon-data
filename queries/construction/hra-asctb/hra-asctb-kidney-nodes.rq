#+ summary: Unique Kidney AS, CT, and B in HRA

PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX kidney: <http://purl.obolibrary.org/obo/UBERON_0002113>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>

SELECT DISTINCT ?iri (SAMPLE(?label) as ?label) (SAMPLE(?type) as ?type) (HRA: as ?source)
FROM HRA:
WHERE {
  VALUES (?asctb_type ?type) {
    ("AS"^^xsd:string <http://purl.obolibrary.org/obo/UBERON_0000061>)
    ("CT"^^xsd:string <http://purl.obolibrary.org/obo/CL_0000000>)
    ("BM"^^xsd:string <http://purl.bioontology.org/ontology/HGNC/gene>)
  }
  ?iri ccf:ccf_asctb_type ?asctb_type ;
    rdfs:label ?label .

  # AS
  {
    ?iri ccf:ccf_part_of ?object .
    ?iri ccf:ccf_part_of* kidney: .
  }
  UNION
  # CT
  {
    [] a ccf:CellMarkerDescriptor ;
      ccf:primary_anatomical_structure ?as ;
      ccf:primary_cell_type ?iri .
    ?as ccf:ccf_part_of* kidney: .
  }
  UNION
  # BM
  {
    [] a ccf:CellMarkerDescriptor ;
      ccf:primary_anatomical_structure ?as ;
      ccf:biomarker ?iri .
    ?as ccf:ccf_part_of* kidney: .
  }
}
GROUP BY ?iri
ORDER BY ?iri
